# Maintainer: anekos <anekos@snca.net>
_pkgname=bonnie++-modern
pkgname=$_pkgname-git
pkgver=20120710.5
pkgrel=1
pkgdesc="Experimental version of a popular hard drive benchmark suite"
arch=('i686' 'x86_64')
url="https://github.com/y-ken/bonnie"
license=('GPL')
depends=('glibc')
makedepends=('git')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("$_pkgname::git+https://github.com/y-ken/bonnie.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  echo "$(git log -1 --format="%cd" --date=short | sed 's|-||g').$(git rev-list --count master)"
}

build() {
  cd "$srcdir/$_pkgname"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make prefix="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
